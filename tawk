#!/usr/bin/env tclsh
# -*- tcl -*-

# Copyright 2020 Shawn Wagner <shawnw.mobile@gmail.com>

# Permission is hereby granted, free of charge, to any person obtaining
# a copy of this software and associated documentation files (the
# "Software"), to deal in the Software without restriction, including
# without limitation the rights to use, copy, modify, merge, publish,
# distribute, sublicense, and/or sell copies of the Software, and to
# permit persons to whom the Software is furnished to do so, subject to
# the following conditions:

# The above copyright notice and this permission notice shall be
# included in all copies or substantial portions of the Software.

# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
# EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
# MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
# NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE
# LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION
# OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION
# WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.

package require Tcl 8.6
package require dicttool
package require textutil::split
package require cmdline
package require csv

set commands [dict create]

proc setup_interp {i} {
    interp share "" stdin $i
    interp share "" stderr $i
    interp share "" stdout $i

    interp alias $i BEGIN {} BEGIN
    interp alias $i END {} END
    interp alias $i BEGINFILE {} BEGINFILE
    interp alias $i ENDFILE {} ENDFILE
    interp alias $i line {} line
    interp alias $i rline {} rline
    interp alias $i csv_join {} ::csv::join
    interp alias $i csv_split {} ::csv::split

    interp eval $i {namespace eval internal {
        proc build_f0_normal {} {
            set new_f0 [lmap i [lrange [lsort -integer [array names ::F]] 1 end] {
                set ::F($i)
            }]
            return [join $new_f0 $::OFS]
        }

        proc build_f0_csv {} {
            set new_f0 [lmap i [lrange [lsort -integer [array names ::F]] 1 end] {
                set ::F($i)
            }]
            return [csv_join $new_f0 $::OFS]
        }

        proc trace_ofs {_ _ op} {
            if {$op eq "unset"} {
                error "Unable to unset OFS"
            }
            if {[array exists ::F]} {
                trace remove variable ::F write ::internal::trace_f
                if {$::CSV} {
                    set ::F(0) [build_f0_csv]
                } else {
                    set ::F(0) [build_f0]
                }
                trace add variable ::F write ::internal::trace_f
            }
        }

        proc trace_f {_ key _} {
            if {![string is integer $key] || $key <= 0} {
                error "Attempt to use out-of-range field index."
            }
            set len [expr {[array size ::F] - 1}]
            trace remove variable ::F write ::internal::trace_f
            if {$key > $len} {
                for {set i [expr {$len + 1}]} {$i < $key} {incr i} {
                    set ::F($i) ""
                }
                trace remove variable ::NF write ::internal::trace_nf
                set ::NF $key
                trace add variable ::NF write ::internal::trace_nf
            }
            if {$::CSV} {
                set ::F(0) [build_f0_csv]
            } else {
                set ::F(0) [build_f0_normal]
            }
            trace add variable ::F write ::internal::trace_f
        }

        proc trace_nf {_ _ _} {
            if {![string is integer $::NF] || $::NF <= 0} {
                error "Attempt to set NF to an out of range value."
            }
            set len [expr {[array size ::F] - 1}]
            trace remove variable ::F write ::internal::trace_f
            if {$len < $::NF} {
                for {set i $len} {$i <= $::NF} {incr i} {
                    set ::F($i) ""
                }
            } elseif {$len > $::NF} {
                for {set i $::NF} {$i > $len} {incr i -1} {
                    unset ::F($i)
                }
            }
            if {$::CSV} {
                set ::F(0) [build_f0_csv]
            } else {
                set ::F(0) [build_f0_normal]
            }
            trace add variable ::F write ::internal::trace_f
        }
        
        proc trace_fs {_ _ _} {
            set ::FS ""
            trace add variable ::FS unset ::internal::trace_fs
        }

        proc trace_raise_error {var _ op} {
            error "Unable to $op protected variable $var"
        }
        
        namespace export trace_*
    }}
}

proc cleanup_interp {i} {
    # Remove toplevel aliases after parsing script
    interp alias $i BEGIN {}
    interp alias $i END {}
    interp alias $i BEGINFILE {} 
    interp alias $i ENDFILE {}
    interp alias $i line {}
    interp alias $i rline {}
}

# Open file and call body for each line read from it.
proc foreach_line {var filename gets body} {
    upvar $var line
    if {$filename eq "-"} {
        set f stdin
    } else {
        set f [open $filename r]
    }
    while {[$gets $f line] >= 0} {
        try {
            uplevel 1 $body
        } on error {msg opts} {
            if {$filename ne "-"} { close $f }
            return -options $opts -level 2 $msg
        } on return {msg opts} {
            if {$filename ne "-"} { close $f }
            return -options $opts -level 2
        } on break {} {
            break
        }
    }
    if {$filename ne "-"} {
        close $f
    }
}

proc foreach_idx {var idx lst bdy} {
    upvar $var v $idx i
    set i 0
    foreach v $lst {
        try {
            uplevel 1 $bdy
        } on error {msg opts} {
            return -options $opts -level 2 $msg
        } on return {msg opts} {
            return -options $opts -level 2 $msg
        } on break {} {
            break
        } finally {
            incr i
        }
    }
}

proc numberlist {lst} {
    set result {}
    foreach_idx f i $lst {
        lappend result [expr {$i + 1}] $f
    }
    return $result
}

proc split_re {line fs} {
    if {$fs eq ""} {
        return [::textutil::split::splitx $line]
    } elseif {[string length $fs] == 1} {
        return [split $line $fs]
    } else {
        return [::textutil::split::splitx $line $fs]
    }
}

proc gets_csv {file linevar} {
    upvar $linevar line
    set line ""
    set totlen 0
    while {[set len [gets $file tmp]] >= 0} {
        incr totlen $len
        if {$line ne ""} {
            set line [string cat $line "\n" $tmp]
        } else {
            set line $tmp
        }
        if {[::csv::iscomplete $line]} {
            return $totlen
        }
    }
    return $len
}

proc split_csv {line fs} {
    if {$fs eq ""} {
        return [::csv::split $line]
    } else {
        return [::csv::split $line $fs]
    }
}

proc process_file {i file gets splitter} {
    variable commands

    interp eval $i [list set ::FILENAME $file]
    interp eval $i {set ::FNR 0}
    foreach script [dict getnull $commands BEGINFILE] {
        interp eval $i [list uplevel #0 $script]
    }
    try {
        foreach_line line $file $gets {
            interp eval $i {incr ::FNR}
            interp eval $i {incr ::NR}
            interp eval $i {array unset ::F}
            interp eval $i {unset ::NF}
            set fs [interp eval $i {set ::FS}]
            set fields [$splitter $line $fs]
            interp eval $i [list set ::NF [llength $fields]]
            interp eval $i [list set ::F(0) $line]
            interp eval $i [list array set ::F [numberlist $fields]]
            set fields [linsert $fields 0 $line]
            interp eval $i {trace add variable ::NF write ::internal::trace_nf}
            interp eval $i {trace add variable ::F write ::internal::trace_f}
            foreach pattern [dict getnull $commands patterns] {
                try {
                    switch [lindex $pattern 0] {
                        expr {
                              lassign $pattern _ test script
                              if {[interp eval $i uplevel #0 expr $test]} {
                                  set script [lindex $pattern 2]
                                  interp eval $i uplevel #0 $script
                              }
                          }
                        re {
                            lassign $pattern _ field re script
                            if {[regexp $re [lindex $fields $field]]} {
                                interp eval $i uplevel #0 $script
                            }
                        }
                    }
                } on error {msg opts} {
                    return -options $opts $msg
                } on break {} {
                    # Skip further processing of the current file
                    return -level 0 -code 5
                } on continue {} {
                    # Skip further processing of the current line
                    break
                }
            }
        }
    } on 5 {} { }
    foreach script [dict getnull $commands ENDFILE] {
        interp eval $i [list uplevel #0 $script]
    }
}

proc tawk {i files gets splitter} {
    variable commands
 
    set nfiles_processed 0
    
    interp eval $i {trace add variable ::OFS {write unset} ::internal::trace_ofs}
    interp eval $i {trace add variable ::CSV {write unset} ::internal::trace_raise_error}
    interp eval $i {set ::NR 0}
    interp eval $i {set ::NF 0}
    
    foreach script [dict getnull $commands BEGIN] {
        interp eval $i [list uplevel #0 $script]
    }
    foreach file $files {
        if {[regexp {^(\w+)=(.*)} $file -> varname varval]} {
            interp eval $i [list set $varname $varval]
            continue
        }
        incr nfiles_processed
        process_file $i $file $gets $splitter
    }
    # Read from standard input if no files given on command line
    if {$nfiles_processed == 0} {
        process_file $i - $gets $splitter
    }
    foreach script [dict getnull $commands END] {
        interp eval $i [list uplevel #0 $script]
    }
}

proc BEGIN {body} {
    variable commands
    dict lappend commands BEGIN $body
}

proc END {body} {
    variable commands
    dict lappend commands END $body
}

proc BEGINFILE {body} {
    variable commands
    dict lappend commands BEGINFILE $body
}

proc ENDFILE {body} {
    variable commands
    dict lappend commands ENDFILE $body
}

proc line args {
    variable commands
    switch [llength $args] {
        1 { dict lappend commands patterns [list expr 1 $args] }
        2 { dict lappend commands patterns [list expr {*}$args] }
        default { error "Invalid number of arguments to line command" }
    }
}

proc rline args {
    variable commands
    set field 0
    if {[llength $args] == 4} {
        set args [lassign $args sw field]
        if {$sw ne "-field"} {
            error "Unknown flag $sw"
        }
    }
    if {[llength $args] != 2} {
        error "rline syntax error"
    }
    lassign $args re body
    dict lappend commands patterns [list re $field $re $body]
}

proc main {argv} {
    set options {
        {f.arg "" "Filename to load instead of script given on the command line."}
        {F.arg "" "Field separator (Regular expression)"}
        {csv "Input is CSV data."}
        {safe "Execute script in safe mode."}
        {timeout.arg 0 "Error if script takes more than N seconds to run (0 for no timeout)." }
    }
    set usage {[OPTIONS] ['script'] [VAR=VALUE | FILENAME] ...}

    try {
        array set params [::cmdline::getoptions argv $options $usage]
        if {[info exists params(safe)] && $params(safe)} {
            set i [interp create -safe]
        } else {
            set i [interp create]
        }
        if {$params(timeout) > 0} {
            interp limit $i time -seconds [expr {[clock seconds] + $params(timeout)}]
        }
        setup_interp $i
        interp eval $i [list set ::FS $params(F)]
        interp eval $i {trace add variable ::FS unset ::internal::trace_fs}
        if {$params(f) ne ""} {
            if {[interp issafe $i]} {
                interp invokehidden $i [list source $params(f)]
            } else {
                interp eval $i [list source $params(f)]
            }
            set files $argv
        } else {
            if {[llength $argv] == 0} {
                error "Missing script argument."
            }
            set files [lassign $argv script]
            interp eval $i $script
        }
        cleanup_interp $i
        if {[info exists params(csv)] && $params(csv)} {
            set csv_mode true
            set gets_cmd gets_csv
            set splitter_cmd split_csv
            interp eval $i {set ::CSV 1}
            interp eval $i {set ::OFS ","}
            interp eval $i {proc print args {
                if {[llength $args] == 0} {
                    puts $F(0)
                } else {
                    puts [csv_join $args $::OFS]
                }
            }}
            interp hide $i gets
            interp alias $i gets {} gets_csv
        } else {
            set csv_mode false
            set gets_cmd gets
            set splitter_cmd split_re
            interp eval $i {set ::CSV 0}
            interp eval $i {set ::OFS " "}
            interp eval $i {proc print args {
                if {[llength $args] == 0} {
                    puts $F(0)
                } else {
                    puts [join $args $::OFS]
                }
            }}
        }
        tawk $i $files $gets_cmd $splitter_cmd
        interp delete $i
    } trap {CMDLINE USAGE} {msg _} {
        puts stderr $msg
        exit 0
    } on error {msg} {
        puts stderr "Error: $msg"
        exit 1
    }
}

main $argv
